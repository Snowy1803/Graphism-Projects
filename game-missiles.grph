#import math
#import random
#import stdio
//shape beginning = Text([Select this text and click 'up' to begin"] 78,200 0 30 BLACK)
//validateAll:
//#while getYForPos[getPosition[beginning]] == 166F // Awaits input
//back = (640,480 WHITE) // RESET
shape presentation = Text(["Avoid missiles"] 226,100 0 30 BLACK)
validate: presentation
shape ball = Circle(150,400 50,50 ORANGE)
validate: ball
pos currentPos = 150,400
integer obsCount = 0
integer currentObstacle = 0
integer minObs = 0
shape obs = Rectangle(0,0 0,0 BLACK)
pos tmpPos = 0,0
integer wave = 1
integer time = 0
#while true
	// Move ball
	setPosition ball: currentPos
	#if getXForPos[getMousePos[this]] < 25 + getXForPos[currentPos]
		translate ball: -1,0
	#elseif getXForPos[getMousePos[this]] > 25 + getXForPos[currentPos]
		translate ball: 1,0
	#if getYForPos[getMousePos[this]] < 25 + getYForPos[currentPos]
		translate ball: 0,-1
	#elseif getYForPos[getMousePos[this]] > 25 + getYForPos[currentPos]
		translate ball: 0,1
	currentPos = getPosition[ball]
	// Corrects position of the ball if outside of the frame
	#if getXForPos[currentPos] < 0
		currentPos = createPos[0 getYForPos[currentPos]]
	#elseif getXForPos[currentPos] > 590
		currentPos = createPos[590 getYForPos[currentPos]]
	#if getYForPos[currentPos] < 0
		currentPos = createPos[getXForPos[currentPos] 0]
	#elseif getYForPos[currentPos] > 430
		currentPos = createPos[getXForPos[currentPos] 430]
	setPosition ball: currentPos
	// Spawns missiles
	#if randomInteger[[300 / wave]] <= 0
		obs = Rectangle(toString["Missile" obsCount] 630,0 30,10 color[128 128 128])
		tmpPos = createPos[630 randomInteger[470]]
		setPosition obs: tmpPos
		validate: obs
		obsCount += 1
	// Move missiles
	currentObstacle = minObs
	#while currentObstacle < obsCount
		obs = getShapeNamed[this toString["Missile" currentObstacle]]
		translate obs: -1,0
		#if getXForPos[getPosition[obs]] < 0 - 30
			unvalidate: obs
			minObs += 1
		#if posAround[getCenterPoint[ball] getCenterPoint[obs] 30]
			unvalidate: presentation
			unvalidate: ball
			back = (640,480 RED)
			shape go = Text(["Game over"] 197,100 0 50 WHITE)
			validate: go
			string s = toString["You" "survived" [divide[getTimeInMillisSinceLoad[this] 1000]] "seconds"]
			shape survTime = Text(s 100,160 0 30 WHITE)
			setHCentered: survTime
			validate: survTime
			#if wave > 2
				s = toString["You" "survived" [wave - 1] "waves"]
			#else
				s = toString["You" "survived" [wave - 1] "wave"]
			shape survWave = Text(s 100,220 0 30 WHITE)
			setHCentered: survWave
			validate: survWave
			end:
		currentObstacle += 1
	//
	// Waves
	#if time >= 10000
		wave = wave + 1
		currentObstacle = minObs
		#while currentObstacle < obsCount
			obs = getShapeNamed[this toString["Missile" currentObstacle]]
			unvalidate: obs
			currentObstacle += 1
		//
		//
		setName presentation: toString["Wave" wave]
		setHCentered: presentation
		obsCount = 0
		minObs = 0
		time = 0
	wait: 2
	time += 1
 //
 //